// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    repositories {
        google()
        mavenCentral()

        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    ext.kotlin_version = '1.6.0'

    dependencies {
        classpath 'com.android.tools.build:gradle:4.1.3'
        classpath 'com.google.gms:google-services:4.3.5'
        classpath 'org.jacoco:org.jacoco.core:0.8.6'
        classpath 'com.google.firebase:firebase-crashlytics-gradle:2.5.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jlleitschuh.gradle:ktlint-gradle:10.0.0"
    }
}

allprojects {
    repositories {
        // Needs to go first to get specialty libraries https://stackoverflow.com/a/48438866/137744
        google()

        mavenCentral()
        maven { url 'https://oss.sonatype.org/content/groups/public' }
        maven { url 'https://jitpack.io' }
        maven {
            url 'https://staging.dev.medicmobile.org/_couch/maven-repo'
            metadataSources { artifact() }
        }

        maven {
            url 'https://api.mapbox.com/downloads/v2/releases/maven'
            authentication {
                basic(BasicAuthentication)
            }
            credentials {
                // Do not change the username below.
                // This should always be `mapbox` (not your username).
                username = "mapbox"
                // Use the secret token you stored in gradle.properties as the password
                password = project.properties['MAPBOX_DOWNLOADS_TOKEN'] ?: ""
            }
        }

        // Local repo for dependencies that only existed in the defunct JCenter repo
        maven {
            url "${rootDir}/jcenter-local-m2"
        }

    }
}

configurations.all {
    transitive = true
}

task checkAll(type: GradleBuild) {
    tasks = ['checkCode', 'checkTests']
}

task checkCode(type: GradleBuild) {
    tasks = ['pmd', 'ktlintCheck', 'checkstyle', 'lintDebug']
}

task checkTests(type: GradleBuild) {
    tasks = ['testDebugUnitTest', 'connectedDebugAndroidTest']
}

task checkFeature(type: GradleBuild) {
    tasks = ["connectedDebugAndroidTest"]
    startParameter.projectProperties = ["android.testInstrumentationRunnerArguments.package": "org.odk.collect.android.feature"]
}

task checkRegression(type: GradleBuild) {
    tasks = ["connectedDebugAndroidTest"]
    startParameter.projectProperties = ["android.testInstrumentationRunnerArguments.package": "org.odk.collect.android.regression"]
}

task checkInstrumented(type: GradleBuild) {
    tasks = ["connectedDebugAndroidTest"]
    startParameter.projectProperties = ["android.testInstrumentationRunnerArguments.package": "org.odk.collect.android.instrumented"]
}

ext {
    androidCompileSdk = 30
    androidMinSdk = 21
    androidTargetSdk = 30

    fragmentVersion = '1.3.6'
    daggerVersion = '2.40'
    okhttp3Version = '4.10.0'
    timberVersion = '4.7.1'
    materialVersion = '1.3.0'
    coreKtxVersion = '1.3.2'
}
