// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    repositories {
        google()
        jcenter()
        maven { url 'https://maven.fabric.io/public' }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.1.2'
        classpath 'com.google.gms:google-services:4.0.1'
        classpath 'org.ow2.asm:asm:6.0' // https://github.com/jacoco/jacoco/issues/639#issuecomment-355424756
        classpath 'org.jacoco:org.jacoco.core:0.8.0'
        classpath 'io.fabric.tools:gradle:1.25.4'
    }
}

plugins {
    id 'com.github.spotbugs' version '1.6.2'
}

allprojects {
    repositories {
        // Needs to go first to get specialty libraries https://stackoverflow.com/a/48438866/137744
        google()

        jcenter()
        maven { url 'https://oss.sonatype.org/content/groups/public' }
        maven { url 'https://jitpack.io' }
        maven { url 'https://staging.dev.medicmobile.org/_couch/maven-repo' }
    }
}

configurations.all {
    transitive = true
}

task checkAll(type: GradleBuild) {
    tasks = ['checkCode', 'checkTests']
}

task checkCode(type: GradleBuild) {
    tasks = ['pmd', 'checkstyle', 'lintDebug', 'spotbugsDebug']
}

task checkTests(type: GradleBuild) {
    tasks = ['testDebugUnitTest', 'connectedDebugAndroidTest']
}

ext {
    supportLibraryVersion = '28.0.0'
    leakyCanaryVersion = '1.5.4'
    daggerVersion = '2.16'
    rxLifecycleVersion = '2.2.1'
    workManagerVersion = "1.0.0-alpha10"
    okhttp3Version = '3.11.0'
}
