apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'com.google.firebase.firebase-perf'
apply from: '../config/quality.gradle'
apply from: '../config/jacoco.gradle'

import com.android.ddmlib.DdmPreferences

DdmPreferences.setTimeOut(60000)

ant.condition(property: 'os', value: 'windows') {
    os(family: 'windows')
}
ant.condition(property: 'os', value: 'unix') {
    os(family: 'unix')
}

// smap delete out the auto setting of version numbers

def secretsFile = file('secrets.properties')
def secrets = new Properties()
if (secretsFile.exists()) {
    secrets.load(new FileInputStream(secretsFile))
}
def googleMapsApiKey = secrets.getProperty('GOOGLE_MAPS_API_KEY', '')
def mapboxToken = secrets.getProperty('MAPBOX_ACCESS_TOKEN', '')

android {
    compileSdkVersion(28)

    defaultConfig {
        applicationId "org.smap.smapTask.android"
        minSdkVersion(21)
        targetSdkVersion(28)
        versionCode(6212)
        versionName getVersionName()
        testInstrumentationRunner('androidx.test.runner.AndroidJUnitRunner')
        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true
        archivesBaseName = 'fieldTask'      // smap
    }

    signingConfigs {
        release {
            if (secrets.getProperty('RELEASE_STORE_FILE')) {
                storeFile file(secrets.getProperty('RELEASE_STORE_FILE'))
                storePassword secrets.getProperty('RELEASE_STORE_PASSWORD')
                keyAlias secrets.getProperty('RELEASE_KEY_ALIAS')
                keyPassword secrets.getProperty('RELEASE_KEY_PASSWORD')
            }
        }
    }

    flavorDimensions "flavour"

    productFlavors {
        standard {
            applicationId "org.smap.smapTask.android"
            versionName "6.212"
        }
        plan {
            applicationId "org.smap.smapTask.android.plan"
            versionName "6.212-plan"
        }
        kontrolid {
            applicationId "org.smap.smapTask.android.kontrolid"
            versionName "6.212-kontrolid"
        }
        supply {
            applicationId "org.smap.smapTask.android.supply"
            versionName "6.212-supply"
        }
        seracis {
            applicationId "org.smap.smapTask.android.seracis"
            versionName "6.212-seracis"
        }
        meqa {
            applicationId "org.smap.smapTask.android.meqa"
            versionName "6.212-meqa"
        }
        atempi {
            applicationId "org.smap.smapTask.android.atempi"
            versionName "6.212-atempi"
        }
        pop {
            applicationId "org.smap.smapTask.android.pop"
            versionName "6.212-pop"
        }
        tdh {
            applicationId "org.smap.smapTask.android.tdh"
            versionName "6.212-tdh"
        }
        assignfield {
            applicationId "org.smap.smapTask.android.assignfield"
            versionName "6.212-assignfield"
        }
        informEd {
            applicationId "org.smap.smapTask.android.informEd"
            versionName "6.212-informEd"
        }
    }

    buildTypes {

        // Release build for all forks
        release {
            buildConfigField("String", "SENDER_ID", "\"" + secrets['SENDER_ID'] + "\"")    // smap
            buildConfigField("String", "AWS_MOBILEHUB_USER_AGENT", "\"" + secrets['AWS_MOBILEHUB_USER_AGENT'] + "\"")   // smap
            buildConfigField("String", "AMAZON_COGNITO_REGION", "\"" + secrets['AMAZON_COGNITO_REGION'] + "\"")         // smap
            buildConfigField("String", "AMAZON_COGNITO_IDENTITY_POOL_ID", "\"" + secrets['AMAZON_COGNITO_IDENTITY_POOL_ID'] + "\"")   // smap
            buildConfigField("String", "AMAZON_DYNAMODB_REGION", "\"" + secrets['AMAZON_DYNAMODB_REGION'] + "\"")  // smap

            if (secrets.getProperty('RELEASE_STORE_FILE')) {
                signingConfig signingConfigs.release
            }
            minifyEnabled(true)
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            resValue("bool", "CRASHLYTICS_ENABLED", "true")
            resValue("string", "GOOGLE_MAPS_API_KEY", googleMapsApiKey)
            buildConfigField 'String', "MAPBOX_ACCESS_TOKEN", '"' + mapboxToken + '"'

            matchingFallbacks = ['release'] // So other modules use release build type for this
        }

        debug {
            buildConfigField("String", "SENDER_ID", "\"" + secrets['SENDER_ID'] + "\"")    // smap
            buildConfigField("String", "AWS_MOBILEHUB_USER_AGENT", "\"" + secrets['AWS_MOBILEHUB_USER_AGENT'] + "\"")
            // smap
            buildConfigField("String", "AMAZON_COGNITO_REGION", "\"" + secrets['AMAZON_COGNITO_REGION'] + "\"")
            // smap
            buildConfigField("String", "AMAZON_COGNITO_IDENTITY_POOL_ID", "\"" + secrets['AMAZON_COGNITO_IDENTITY_POOL_ID'] + "\"")
            // smap
            buildConfigField("String", "AMAZON_DYNAMODB_REGION", "\"" + secrets['AMAZON_DYNAMODB_REGION'] + "\"")
            // smap


            debuggable(true)
            testCoverageEnabled((findProperty("enableJacoco") ?: "false").toBoolean())
            resValue("bool", "CRASHLYTICS_ENABLED", "false")
            resValue("string", "GOOGLE_MAPS_API_KEY", googleMapsApiKey)
            buildConfigField 'String', "MAPBOX_ACCESS_TOKEN", '"' + mapboxToken + '"'
            //FirebasePerformance {    // smap commented
            //    instrumentationEnabled false
            //}
        }
    }

    // https://stackoverflow.com/a/27119543/152938
    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            outputFileName = new File(outputFileName.replace(".apk", "-${defaultConfig.versionName}.apk"))
        }
    }
    testVariants.all { variant ->
        variant.outputs.all { output ->
            outputFileName = new File(outputFileName.replace(".apk", "-${defaultConfig.versionName}.apk"))
        }
    }

    packagingOptions {
        // Pick first occurrence of any files that cause conflicts, as defined
        // in common.gradle
        pickFirst 'META-INF/DEPENDENCIES'
        pickFirst 'META-INF/LICENSE'
        pickFirst 'META-INF/LICENSE.txt'
        pickFirst 'META-INF/NOTICE'
        pickFirst 'META-INF/NOTICE.txt'
        pickFirst 'META-INF/services/com.fasterxml.jackson.core.JsonFactory'
        pickFirst 'META-INF/services/com.fasterxml.jackson.core.ObjectCodec'
        pickFirst 'META-INF/services/javax.ws.rs.ext.MessageBodyReader'
        pickFirst 'META-INF/services/javax.ws.rs.ext.MessageBodyWriter'

        // To ensure that ODK Collect is installable on all devices, we don't use
        // abiFilters to exclude any ABIs; but to keep the APK slim, we include
        // the Mapbox native library only for 32-bit and 64-bit ARM devices and
        // omit it for all X86 devices.
        exclude 'lib/x86/libmapbox-gl.so'
        exclude 'lib/x86_64/libmapbox-gl.so'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dexOptions {
        javaMaxHeapSize '2048M'
    }

    lintOptions {
        disable 'MissingTranslation', 'ExtraTranslation'    // smap
        checkReleaseBuilds false
        abortOnError true
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
            returnDefaultValues = true
            all {
                // https://discuss.circleci.com/t/11207/24
                // it seems any number works, but 1024 - 2048 seem reasonable
                maxHeapSize = "2048M"
            }
        }
    }
}

configurations.all {
    resolutionStrategy {
        cacheDynamicVersionsFor(0, 'seconds')
        cacheChangingModulesFor(0, 'seconds')

        force('com.google.code.findbugs:jsr305:1.3.9')
    }
    transitive = true
}

dependencies {
    implementation 'androidx.legacy:legacy-support-v13:1.0.0'
    implementation 'androidx.browser:browser:1.2.0'
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation "androidx.work:work-runtime:2.3.4"
    implementation 'com.google.android.material:material:1.1.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.exifinterface:exifinterface:1.1.0'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.preference:preference:1.1.0'
    implementation "androidx.fragment:fragment:${rootProject.fragmentVersion}"

    implementation "com.google.android.gms:play-services-auth:17.0.0"
    implementation "com.google.android.gms:play-services-maps:17.0.0"
    implementation "com.google.android.gms:play-services-location:17.0.0"
    //implementation "com.google.android.gms:play-services-gcm:17.0.0"    // smap google cloud messaging
    implementation "com.google.firebase:firebase-messaging:20.2.4"

    implementation 'com.google.firebase:firebase-perf:19.0.5'
    implementation 'com.google.firebase:firebase-analytics:17.2.3'
    implementation "com.crashlytics.sdk.android:crashlytics:2.10.1"

    implementation 'com.mapbox.mapboxsdk:mapbox-android-sdk:9.0.0'
    implementation 'com.mapbox.mapboxsdk:mapbox-android-plugin-annotation-v9:0.8.0'

    implementation("com.google.guava:guava:28.2-android")

    implementation "com.squareup.okhttp3:okhttp:${rootProject.okhttp3Version}"
    implementation 'com.burgstaller:okhttp-digest:2.2'

    implementation 'com.github.mohamadian:PersianJodaTime:1.2'
    implementation 'com.github.chanmratekoko:myanmar-calendar:1.0.6.RC3'
    implementation 'bikramsambat:bikram-sambat:1.1.0'
    implementation "com.evernote:android-job:1.4.2"
    implementation "com.rarepebble:colorpicker:3.0.1"
    implementation "commons-io:commons-io:2.6"
    implementation "net.sf.opencsv:opencsv:2.4"
    // smap comment out reference to maven repository version of javarosa will use a local version instead
    //implementation("org.opendatakit:opendatakit-javarosa:2.17.1") {
    //    exclude group: 'joda-time'
    //    exclude group: 'org.slf4j'
    //}
    implementation('net.sf.kxml:kxml2:2.3.0')                       // smap javarosa dependency
    implementation files('libs/opendatakit-javarosa-2.17.1.jar')    // smap javarosa


    implementation "com.karumi:dexter:6.0.2"
    implementation "org.osmdroid:osmdroid-android:6.1.6"
    implementation("com.google.api-client:google-api-client-android:1.30.9") {
        exclude group: 'org.apache.httpcomponents'
        exclude group: 'com.google.guava'
    }
    implementation("com.google.apis:google-api-services-drive:v3-rev20200306-1.30.9") {
        exclude group: 'org.apache.httpcomponents'
        exclude group: 'com.google.guava'
    }
    implementation("com.google.apis:google-api-services-sheets:v4-rev20200312-1.30.9") {
        exclude group: 'org.apache.httpcomponents'
        exclude group: 'com.google.guava'
    }

    implementation "com.jakewharton.timber:timber:4.7.1"
    implementation "org.slf4j:slf4j-api:2.0.0-alpha0"
    implementation "com.arcao:slf4j-timber:3.1@aar"

    implementation "com.google.zxing:core:3.3.0" //updating to 3.4.0 requires minSdkVersion >=24 https://github.com/zxing/zxing/releases/tag/zxing-3.4.0
    implementation "com.journeyapps:zxing-android-embedded:3.6.0" //updating would require minSdkVersion >=24
    implementation "net.danlew:android.joda:2.10.3"

    // Real LeakCanary for debug builds only: notifications, analysis, etc
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.2'

    // Android Architecture Components:
    implementation "androidx.lifecycle:lifecycle-extensions:2.2.0"
    //implementation "androidx.lifecycle:lifecycle-viewmodel:2.2.0"     // smap
    implementation "androidx.viewpager2:viewpager2:1.0.0"

    // smap aws
    implementation 'com.amazonaws:aws-android-sdk-core:2.16.+'
    //implementation 'com.amazonaws:aws-android-sdk-core:2.4.2'
    // add the DynamoDB SDK and DynamoDB Object Mapper dependency
    implementation 'com.amazonaws:aws-android-sdk-ddb:2.16.+'
    implementation 'com.amazonaws:aws-android-sdk-ddb-mapper:2.16.+'
    implementation project(':AWSMobileHelper')

    // charts
    implementation 'com.github.PhilJay:MPAndroidChart:v3.0.2'

    // Dagger:
    implementation "com.google.dagger:dagger:${rootProject.daggerVersion}"
    annotationProcessor "com.google.dagger:dagger-compiler:${rootProject.daggerVersion}"
    testAnnotationProcessor "com.google.dagger:dagger-compiler:${rootProject.daggerVersion}" // Needed for injection into Robolectric tests

    // Dagger Android:
    implementation "com.google.dagger:dagger-android:${rootProject.daggerVersion}"
    implementation "com.google.dagger:dagger-android-support:${rootProject.daggerVersion}"
    annotationProcessor "com.google.dagger:dagger-android-processor:${rootProject.daggerVersion}"

    // RxJava 2:
    implementation "io.reactivex.rxjava2:rxandroid:2.1.1"
    implementation "io.reactivex.rxjava2:rxjava:2.2.19"

    // Better "Subjects" for Rx:
    implementation "com.jakewharton.rxrelay2:rxrelay:2.1.1"

    implementation project(':nbi-stubs')
    implementation project(':material')

    // Makes binding to Views easy:
    implementation "com.jakewharton:butterknife:10.2.1"
    annotationProcessor "com.jakewharton:butterknife-compiler:10.2.1"

    // Annotations understood by FindBugs
    compileOnly 'com.google.code.findbugs:annotations:3.0.1'
    compileOnly 'com.google.code.findbugs:jsr305:3.0.2'

    // Used to generate documentation screenshots.
    androidTestImplementation "tools.fastlane:screengrab:2.0.0"

    // Testing-only dependencies
    testImplementation "junit:junit:4.13"
    testImplementation "org.mockito:mockito-core:3.3.3"

    testImplementation "org.robolectric:robolectric:4.3.1"
    testImplementation 'androidx.test.ext:junit:1.1.1'
    debugImplementation "androidx.fragment:fragment-testing:${rootProject.fragmentVersion}"
    testImplementation "org.robolectric:shadows-multidex:4.3.1"
    testImplementation "com.squareup.okhttp3:mockwebserver:${rootProject.okhttp3Version}"
    testImplementation "com.squareup.okhttp3:okhttp-tls:${rootProject.okhttp3Version}"

    // power mock (for mocking final methods which is not handled by mockito)
    testImplementation "org.powermock:powermock-module-junit4:2.0.5"
    testImplementation "org.powermock:powermock-api-mockito2:2.0.5"

    testImplementation "org.json:json:20190722"

    androidTestImplementation "org.mockito:mockito-android:3.3.3"
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    androidTestImplementation "androidx.arch.core:core-testing:2.1.0"
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.2.0'
    androidTestImplementation("androidx.test.espresso:espresso-intents:3.2.0") {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
    }

    androidTestImplementation "com.squareup.okhttp3:mockwebserver:${rootProject.okhttp3Version}"

    //androidTestImplementation "android.arch.work:work-testing:1.0.1"

    // smap
    //implementation "com.android.support:design:28.2.0"
    androidTestImplementation "androidx.work:work-testing:2.3.4"
}

// Must be at bottom to prevent dependency collisions
// https://developers.google.com/android/guides/google-services-plugin
apply plugin: 'com.google.gms.google-services'



