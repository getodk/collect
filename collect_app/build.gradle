apply plugin: 'com.android.application'
apply from: '../config/quality.gradle'

import com.android.ddmlib.DdmPreferences
DdmPreferences.setTimeOut(60000)

ant.condition(property: 'os', value: 'windows') {
    os(family: 'windows')
}
ant.condition(property: 'os', value: 'unix') {
    os(family: 'unix')
}

// Build numbers were manually set until 1067
def LEGACY_BUILD_NUMBER_OFFSET = 1067

// Based on http://stackoverflow.com/questions/17097263#24121734
def getMasterCommitCount = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            switch (ant.properties.os) {
                case 'windows':
                    commandLine 'cmd', '/c', 'git', 'rev-list', '--first-parent', '--count', 'master'
                    break
                case 'unix':
                    commandLine 'git', 'rev-list', '--first-parent', '--count', 'master'
                    break
            }
            standardOutput = stdout
        }
        return Integer.parseInt(stdout.toString().trim())
    } catch (ignored) {
        return -1;
    }
}

def getVersionName = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            switch (ant.properties.os) {
                case 'windows':
                    commandLine 'cmd', '/c', 'git', 'describe', '--tags', '--dirty', '--always'
                    break
                case 'unix':
                    commandLine 'git', 'describe', '--tags', '--dirty', '--always'
                    break
            }
            standardOutput = stdout
        }
        return stdout.toString().trim()
    } catch (ignored) {
        return null;
    }
}

def secretsFile = file('secrets.properties')
def secrets = new Properties()
if (secretsFile.exists()) {
    secrets.load(new FileInputStream(secretsFile))
}

android {
    compileSdkVersion(25)
    buildToolsVersion('26.0.1')

    defaultConfig {
        applicationId('org.odk.collect.android')
        minSdkVersion(16)
        targetSdkVersion(22)
        versionCode LEGACY_BUILD_NUMBER_OFFSET + getMasterCommitCount()
        versionName getVersionName()
        testInstrumentationRunner('android.support.test.runner.AndroidJUnitRunner')
        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true
    }

    signingConfigs {
        release {
            if (secrets.getProperty('RELEASE_STORE_FILE')) {
                storeFile file(secrets.getProperty('RELEASE_STORE_FILE'))
                storePassword secrets.getProperty('RELEASE_STORE_PASSWORD')
                keyAlias secrets.getProperty('RELEASE_KEY_ALIAS')
                keyPassword secrets.getProperty('RELEASE_KEY_PASSWORD')
            }
        }
    }

    buildTypes {
        // Release build for all forks
        release {
            if (secrets.getProperty('RELEASE_STORE_FILE')) {
                signingConfig signingConfigs.release
            }
            minifyEnabled(true)
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
        // Release build for the original ODK Collect app
        odkCollectRelease {
            if (secrets.getProperty('RELEASE_STORE_FILE')) {
                signingConfig signingConfigs.release
            }
            minifyEnabled(true)
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
        debug {
            debuggable(true)
            // Allows AndroidTest JaCoCo reports to be generated
            testCoverageEnabled(true)
        }
    }

    // https://stackoverflow.com/a/27119543/152938
    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def file = output.outputFile
            output.outputFile = new File(file.parent, file.name.replace("_app","").replace(".apk", "-" + defaultConfig.versionName + ".apk"))
        }
    }

    packagingOptions {
        // Pick first occurrence of any files that cause conflicts, as defined
        // in common.gradle
        pickFirst 'META-INF/DEPENDENCIES'
        pickFirst 'META-INF/LICENSE'
        pickFirst 'META-INF/LICENSE.txt'
        pickFirst 'META-INF/NOTICE'
        pickFirst 'META-INF/NOTICE.txt'
        pickFirst 'META-INF/services/com.fasterxml.jackson.core.JsonFactory'
        pickFirst 'META-INF/services/com.fasterxml.jackson.core.ObjectCodec'
        pickFirst 'META-INF/services/javax.ws.rs.ext.MessageBodyReader'
        pickFirst 'META-INF/services/javax.ws.rs.ext.MessageBodyWriter'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    dexOptions {
        javaMaxHeapSize '2048M'
    }
}

configurations.all {
    resolutionStrategy {
        cacheDynamicVersionsFor(0, 'seconds')
        cacheChangingModulesFor(0, 'seconds')
    }
    transitive = true
}

allprojects {
    tasks.withType(JavaCompile) {
        options.compilerArgs << '-Xlint:all'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: '*.jar')
    compile group: 'com.android.support', name: 'appcompat-v7', version: '25.3.1'
    compile group: 'com.android.support', name: 'design', version: '25.3.1'
    compile group: 'com.android.support', name: 'cardview-v7', version: '25.3.1'
    compile group: 'com.android.support', name: 'multidex', version: '1.0.1'
    compile group: 'com.google.android.gms', name: 'play-services-analytics', version: '10.0.1'
    compile group: 'com.google.android.gms', name: 'play-services-auth', version: '10.0.1'
    compile group: 'com.google.android.gms', name: 'play-services-maps', version: '10.0.1'
    compile group: 'com.google.android.gms', name: 'play-services-location', version: '10.0.1'
    compile (group: 'com.google.code.gson', name: 'gson', version: '2.6.2'){
        exclude group: 'org.apache.httpcomponents'
    }
    compile group: 'com.google.firebase', name: 'firebase-core', version: '10.0.1'
    compile group: 'com.google.firebase', name: 'firebase-crash', version: '10.0.1'
    compile (group: 'com.google.http-client', name: 'google-http-client', version: '1.22.0') {
        exclude group: 'org.apache.httpcomponents'
    }
    compile (group: 'com.google.oauth-client', name: 'google-oauth-client', version: '1.22.0') {
        exclude group: 'org.apache.httpcomponents'
    }

    compile group: 'commons-io', name: 'commons-io', version: '2.4'
    compile group: 'net.sf.kxml', name: 'kxml2', version: '2.3.0'
    compile group: 'net.sf.opencsv', name: 'opencsv', version: '2.3'
    compile (group: 'org.opendatakit', name: 'opendatakit-javarosa', version: '2.6.1') {
        exclude module: 'joda-time'
    }
    compile group: 'org.osmdroid', name: 'osmdroid-android', version: '5.6.4'
    compile group: 'org.slf4j', name: 'slf4j-android', version: '1.6.1-RC1'
    compile group: 'pub.devrel', name: 'easypermissions', version: '0.2.1'
    compile(group: 'com.google.api-client', name: 'google-api-client-android', version: '1.22.0') { exclude group: 'org.apache.httpcomponents' }
    compile(group: 'com.google.apis', name: 'google-api-services-drive', version: 'v3-rev64-1.22.0') { exclude group: 'org.apache.httpcomponents' }
    compile(group: 'com.google.apis', name: 'google-api-services-sheets', version: 'v4-rev463-1.22.0') { exclude group: 'org.apache.httpcomponents' }
    compile group: 'com.jakewharton.timber', name: 'timber', version: '4.5.1'
    compile group: 'com.android.support', name: 'customtabs', version: '25.3.1'
    compile group: 'com.android.support', name: 'design', version: '25.3.1'
    compile group: 'com.google.zxing', name: 'core', version: '3.3.0'
    compile group: 'com.journeyapps', name: 'zxing-android-embedded', version: '3.5.0'
    compile group: 'com.android.support', name: 'support-v13', version: '25.3.1'
    compile group: 'net.danlew', name: 'android.joda', version: '2.9.9'

    // Testing-only dependencies
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '2.8.47'
    testCompile group: 'org.robolectric', name: 'robolectric', version: '3.4'
    testCompile group: 'org.robolectric', name: 'shadows-multidex', version: '3.3.2'

    androidTestCompile group: 'org.mockito', name: 'mockito-android', version: '2.8.47'
    androidTestCompile group: 'com.android.support', name: 'support-annotations', version: '25.3.1'
    androidTestCompile group: 'com.android.support.test', name: 'runner', version: '0.5'
    androidTestCompile group: 'com.android.support.test', name: 'rules', version: '0.5'
    androidTestCompile group: 'com.squareup.okhttp3', name: 'mockwebserver', version: '3.2.0'
}

// Must be at bottom to prevent dependency collisions
// https://developers.google.com/android/guides/google-services-plugin
apply plugin: 'com.google.gms.google-services'
