apply plugin: 'com.android.application'
apply from: '../config/quality.gradle'
apply from: '../config/jacoco.gradle'
apply plugin: 'kotlin-android'

import com.android.ddmlib.DdmPreferences
import dependencies.Dependencies
import dependencies.Versions

DdmPreferences.setTimeOut(60000)

ant.condition(property: 'os', value: 'windows') {
    os(family: 'windows')
}
ant.condition(property: 'os', value: 'unix') {
    os(family: 'unix')
}

// Build numbers were manually set until 1067
def LEGACY_BUILD_NUMBER_OFFSET = 1067

// Based on http://stackoverflow.com/questions/17097263#24121734
def getMasterCommitCount = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            switch (ant.properties.os) {
                case 'windows':
                    commandLine 'cmd', '/c', 'git', 'rev-list', '--first-parent', '--count', 'master'
                    break
                case 'unix':
                    commandLine 'git', 'rev-list', '--first-parent', '--count', 'master'
                    break
            }
            standardOutput = stdout
        }
        return Integer.parseInt(stdout.toString().trim())
    } catch (ignored) {
        return -1
    }
}

def getVersionName = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            switch (ant.properties.os) {
                case 'windows':
                    commandLine 'cmd', '/c', 'git', 'describe', '--tags', '--dirty', '--always'
                    break
                case 'unix':
                    commandLine 'git', 'describe', '--tags', '--dirty', '--always'
                    break
            }
            standardOutput = stdout
        }
        return stdout.toString().trim()
    } catch (ignored) {
        return null
    }
}

def secretsFile = file('secrets.properties')
def secrets = new Properties()
if (secretsFile.exists()) {
    secrets.load(new FileInputStream(secretsFile))
}
def googleMapsApiKey = secrets.getProperty('GOOGLE_MAPS_API_KEY', '')
def mapboxToken = secrets.getProperty('MAPBOX_ACCESS_TOKEN', '')

android {
    compileSdkVersion Versions.android_compile_sdk

    buildFeatures {
        viewBinding true
    }

    defaultConfig {
        applicationId('org.odk.collect.android')
        minSdkVersion Versions.android_min_sdk
        targetSdkVersion Versions.android_target_sdk
        versionCode LEGACY_BUILD_NUMBER_OFFSET + getMasterCommitCount()
        versionName getVersionName()
        testInstrumentationRunner('androidx.test.runner.AndroidJUnitRunner')
        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true
        archivesBaseName = 'collect'
    }

    signingConfigs {
        release {
            if (secrets.getProperty('RELEASE_STORE_FILE')) {
                storeFile file(secrets.getProperty('RELEASE_STORE_FILE'))
                storePassword secrets.getProperty('RELEASE_STORE_PASSWORD')
                keyAlias secrets.getProperty('RELEASE_KEY_ALIAS')
                keyPassword secrets.getProperty('RELEASE_KEY_PASSWORD')
            }
        }

        selfSignedRelease {
            storeFile file('../debug.keystore')
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
    }

    buildTypes {

        // Release build for all forks
        release {
            if (secrets.getProperty('RELEASE_STORE_FILE')) {
                signingConfig signingConfigs.release
            }
            minifyEnabled(true)
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            resValue("string", "GOOGLE_MAPS_API_KEY", googleMapsApiKey)
            buildConfigField 'String', "MAPBOX_ACCESS_TOKEN", '"' + mapboxToken + '"'
        }

        // Release build for the official ODK Collect app
        odkCollectRelease {
            if (secrets.getProperty('RELEASE_STORE_FILE')) {
                signingConfig signingConfigs.release
            }
            minifyEnabled(true)
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            resValue("string", "GOOGLE_MAPS_API_KEY", googleMapsApiKey)
            buildConfigField 'String', "MAPBOX_ACCESS_TOKEN", '"' + mapboxToken + '"'

            matchingFallbacks = ['release'] // So other modules use release build type for this
        }

        selfSignedRelease {
            signingConfig signingConfigs.selfSignedRelease

            minifyEnabled(true)
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            resValue("string", "GOOGLE_MAPS_API_KEY", googleMapsApiKey)
            buildConfigField 'String', "MAPBOX_ACCESS_TOKEN", '"' + mapboxToken + '"'

            matchingFallbacks = ['release'] // So other modules use release build type for this
        }

        debug {
            debuggable(true)
            testCoverageEnabled((findProperty("enableJacoco") ?: "false").toBoolean())
            resValue("string", "GOOGLE_MAPS_API_KEY", googleMapsApiKey)
            buildConfigField 'String', "MAPBOX_ACCESS_TOKEN", '"' + mapboxToken + '"'
        }
    }

    // https://stackoverflow.com/a/27119543/152938
    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            outputFileName = new File(outputFileName.replace(".apk", "-${defaultConfig.versionName}.apk"))
        }
    }
    testVariants.all { variant ->
        variant.outputs.all { output ->
            outputFileName = new File(outputFileName.replace(".apk", "-${defaultConfig.versionName}.apk"))
        }
    }

    packagingOptions {
        // Pick first occurrence of any files that cause conflicts, as defined
        // in common.gradle
        pickFirst 'META-INF/DEPENDENCIES'
        pickFirst 'META-INF/LICENSE'
        pickFirst 'META-INF/LICENSE.txt'
        pickFirst 'META-INF/NOTICE'
        pickFirst 'META-INF/NOTICE.txt'
        pickFirst 'META-INF/services/com.fasterxml.jackson.core.JsonFactory'
        pickFirst 'META-INF/services/com.fasterxml.jackson.core.ObjectCodec'
        pickFirst 'META-INF/services/javax.ws.rs.ext.MessageBodyReader'
        pickFirst 'META-INF/services/javax.ws.rs.ext.MessageBodyWriter'

        // To ensure that ODK Collect is installable on all devices, we don't use
        // abiFilters to exclude any ABIs; but to keep the APK slim, we include
        // the Mapbox native library only for 32-bit and 64-bit ARM devices and
        // omit it for all X86 devices.
        exclude 'lib/x86/libmapbox-gl.so'
        exclude 'lib/x86_64/libmapbox-gl.so'
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
            returnDefaultValues = true
        }
    }

    lintOptions {
        abortOnError true
        xmlReport true
        htmlReport true
        checkGeneratedSources true
        checkDependencies true
        lintConfig file("${project.rootDir}/config/lint.xml")
    }

    sourceSets {
        androidTest {
            java.srcDirs += "src/commonTest/java"
        }
        test {
            java.srcDirs += "src/commonTest/java"
        }
    }
}

configurations.all {
    resolutionStrategy {
        cacheDynamicVersionsFor(0, 'seconds')
        cacheChangingModulesFor(0, 'seconds')

        force 'org.objenesis:objenesis:2.6'
    }
    transitive = true
}

dependencies {
    repositories {
        mavenLocal() // Only used for javarosa_local dependency
    }

    coreLibraryDesugaring Dependencies.desugar

    implementation project(':shared')
    implementation project(':androidshared')
    implementation project(':nbistubs')
    implementation project(':strings')
    implementation project(':material')
    implementation project(':async')
    implementation project(':analytics')
    implementation project(':audioclips')
    implementation project(':forms')
    implementation project(':audiorecorder')
    implementation project(':projects')
    implementation project(':location')
    implementation project(':geo')
    implementation project(':glide')
    implementation project(':errors')
    implementation project(':upgrade')
    implementation project(':permissions')

    implementation Dependencies.androidx_startup
    implementation Dependencies.androidx_legacy_support
    implementation Dependencies.androidx_core_ktx
    implementation Dependencies.androidx_browser
    implementation Dependencies.androidx_appcompat
    implementation Dependencies.androidx_work_runtime

    implementation Dependencies.androidx_cardview
    implementation Dependencies.androidx_exinterface
    implementation Dependencies.androidx_multidex
    implementation Dependencies.androidx_preference_ktx
    implementation Dependencies.androidx_fragment_ktx

    implementation Dependencies.android_material
    implementation Dependencies.android_flexbox

    implementation Dependencies.play_services_auth
    implementation Dependencies.play_services_maps
    implementation Dependencies.play_services_location

    implementation Dependencies.firebase_analytics
    implementation Dependencies.firebase_crashlytics

    implementation Dependencies.mapbox_android_sdk
    implementation Dependencies.mapbox_android_telemetry
    implementation Dependencies.mapbox_android_plugin_annotation

    implementation Dependencies.guava

    implementation Dependencies.squareup_okhttp
    implementation Dependencies.squareup_okhttp_tls
    implementation Dependencies.burgstaller_okhttp_digest
    
    implementation Dependencies.persian_joda_time
    implementation Dependencies.myanmar_calendar
    implementation Dependencies.bikram_sambat
    implementation Dependencies.rarepebble_colorpicker
    implementation Dependencies.commons_io
    implementation Dependencies.opencsv
    implementation(Dependencies.javarosa) {
        exclude group: 'joda-time'
        exclude group: 'org.slf4j'
        exclude group: 'org.hamcrest', module: 'hamcrest-all'
    }
    implementation Dependencies.osmdroid
    implementation(Dependencies.google_api_client_android) {
        exclude group: 'org.apache.httpcomponents'
        exclude group: 'com.google.guava'
    }
    implementation(Dependencies.google_api_services_drive) {
        exclude group: 'org.apache.httpcomponents'
        exclude group: 'com.google.guava'
    }
    implementation(Dependencies.google_api_services_sheets) {
        exclude group: 'org.apache.httpcomponents'
        exclude group: 'com.google.guava'
    }

    implementation Dependencies.timber
    implementation Dependencies.slf4j_api
    implementation Dependencies.slf4j_timber

    implementation Dependencies.zxing_android_embedded

    implementation Dependencies.danlew_android_joda

    // Android Architecture Components:
    implementation Dependencies.androidx_lifecycle_extensions
    implementation Dependencies.androidx_lifecycle_runtime_ktx
    implementation Dependencies.androidx_viewpager2

    // Dagger:
    implementation Dependencies.dagger
    annotationProcessor Dependencies.dagger_compiler

    // Needed for injection into Robolectric tests
    testAnnotationProcessor Dependencies.dagger_compiler

    // Dagger Android:
    implementation Dependencies.dagger_android
    implementation Dependencies.dagger_android_support
    annotationProcessor Dependencies.dagger_android_processor

    // RxJava 2:
    implementation Dependencies.rxandroid
    implementation Dependencies.rxjava

    // Better "Subjects" for Rx:
    implementation Dependencies.rxrelay

    implementation Dependencies.armen101_audio_recorder_view

    // Glide
    implementation Dependencies.glide

    implementation(Dependencies.mp4parser_muxer) {
        exclude group: 'org.hamcrest'
    }

    testImplementation project(':formstest')

    // Testing-only dependencies
    testImplementation Dependencies.junit
    testImplementation Dependencies.mockito_core
    testImplementation Dependencies.mockito_inline
    testImplementation Dependencies.mockito_kotlin

    testImplementation(project(path: ':testshared')) {
        exclude group: 'org.robolectric' // Some tests in `collect_app` don't work with newer Robolectric
    }

    testImplementation Dependencies.robolectric

    testImplementation Dependencies.androidx_test_ext_junit
    testImplementation Dependencies.androidx_arch_core_testing
    testImplementation Dependencies.robolectric_shadows_multidex
    testImplementation Dependencies.okhttp3_mockwebserver
    testImplementation Dependencies.squareup_okhttp_tls

    testImplementation Dependencies.hamcrest

    testImplementation Dependencies.json
    testImplementation Dependencies.androidx_test_espresso_core
    testImplementation Dependencies.androidx_test_espresso_intents
    testImplementation Dependencies.androidx_test_core_ktx
    testImplementation Dependencies.kotlinx_coroutines_test

    // Used to generate documentation screenshots.
    androidTestImplementation Dependencies.fastlane_screengrab
    androidTestImplementation Dependencies.mockito_android
    androidTestImplementation Dependencies.androidx_test_ext_junit
    androidTestImplementation Dependencies.androidx_test_espresso_core
    androidTestImplementation Dependencies.androidx_arch_core_testing
    androidTestImplementation Dependencies.androidx_test_espresso_contrib
    androidTestImplementation(Dependencies.androidx_test_espresso_intents) {
        exclude group: 'com.android.support', module: 'support-annotations'
    }

    androidTestImplementation Dependencies.okhttp3_mockwebserver
    androidTestImplementation Dependencies.androidx_work_testing
    androidTestImplementation Dependencies.uiautomator

    debugImplementation project(':fragmentstest')
    debugImplementation(project(':testshared')) {
        exclude group: 'org.robolectric' // Some tests in `collect_app` don't work with newer Robolectric
    }

    // Real LeakCanary for debug builds only: notifications, analysis, etc
    debugImplementation Dependencies.leakcanary
}

// Must be at bottom to prevent dependency collisions
// https://developers.google.com/android/guides/google-services-plugin
apply plugin: 'com.google.gms.google-services'

// Must be after google-services to prevent build failure
// https://stackoverflow.com/a/67995305
apply plugin: 'com.google.firebase.crashlytics'
