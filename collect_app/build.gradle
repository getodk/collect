apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply from: '../config/quality.gradle'
apply from: '../config/jacoco.gradle'

import com.android.ddmlib.DdmPreferences

DdmPreferences.setTimeOut(60000)

ant.condition(property: 'os', value: 'windows') {
    os(family: 'windows')
}
ant.condition(property: 'os', value: 'unix') {
    os(family: 'unix')
}

// Build numbers were manually set until 1067
def LEGACY_BUILD_NUMBER_OFFSET = 1067

// Based on http://stackoverflow.com/questions/17097263#24121734
def getMasterCommitCount = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            switch (ant.properties.os) {
                case 'windows':
                    commandLine 'cmd', '/c', 'git', 'rev-list', '--first-parent', '--count', 'master'
                    break
                case 'unix':
                    commandLine 'git', 'rev-list', '--first-parent', '--count', 'master'
                    break
            }
            standardOutput = stdout
        }
        return Integer.parseInt(stdout.toString().trim())
    } catch (ignored) {
        return -1
    }
}

def getVersionName = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            switch (ant.properties.os) {
                case 'windows':
                    commandLine 'cmd', '/c', 'git', 'describe', '--tags', '--dirty', '--always'
                    break
                case 'unix':
                    commandLine 'git', 'describe', '--tags', '--dirty', '--always'
                    break
            }
            standardOutput = stdout
        }
        return stdout.toString().trim()
    } catch (ignored) {
        return null
    }
}

def getContributorList = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            switch (ant.properties.os) {
                case 'windows':
                    commandLine 'cmd', '/c', 'git', 'shortlog', '-nse', 'master'
                    break
                case 'unix':
                    commandLine 'git', 'shortlog', '-nse', 'master'
                    break
            }
            standardOutput = stdout
        }
        return stdout.toString().trim().split("\\n")
    } catch (ignored) {
        return null
    }
}

def secretsFile = file('secrets.properties')
def secrets = new Properties()
if (secretsFile.exists()) {
    secrets.load(new FileInputStream(secretsFile))
}

android {
    compileSdkVersion(27)
    buildToolsVersion('26.0.2')

    defaultConfig {
        applicationId('org.odk.collect.android')
        minSdkVersion(16)
        targetSdkVersion(22)
        versionCode LEGACY_BUILD_NUMBER_OFFSET + getMasterCommitCount()
        versionName getVersionName()
        testInstrumentationRunner('android.support.test.runner.AndroidJUnitRunner')
        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true
        archivesBaseName = 'collect'
        buildConfigField "String", "CONTRIBUTOR_LIST", "\"${getContributorList}\""
    }

    signingConfigs {
        release {
            if (secrets.getProperty('RELEASE_STORE_FILE')) {
                storeFile file(secrets.getProperty('RELEASE_STORE_FILE'))
                storePassword secrets.getProperty('RELEASE_STORE_PASSWORD')
                keyAlias secrets.getProperty('RELEASE_KEY_ALIAS')
                keyPassword secrets.getProperty('RELEASE_KEY_PASSWORD')
            }
        }
    }

    buildTypes {
        // Release build for all forks
        release {
            if (secrets.getProperty('RELEASE_STORE_FILE')) {
                signingConfig signingConfigs.release
            }
            minifyEnabled(true)
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            resValue("bool", "FIREBASE_CRASHLYTICS_ENABLED", "false")
        }
        // Release build for the original ODK Collect app
        odkCollectRelease {
            if (secrets.getProperty('RELEASE_STORE_FILE')) {
                signingConfig signingConfigs.release
            }
            minifyEnabled(true)
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            resValue("bool", "FIREBASE_CRASHLYTICS_ENABLED", "true")
        }
        debug {
            debuggable(true)
            testCoverageEnabled(true)
            resValue("bool", "FIREBASE_CRASHLYTICS_ENABLED", "false")
        }
    }

    // https://stackoverflow.com/a/27119543/152938
    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            outputFileName = new File(outputFileName.replace(".apk", "-${defaultConfig.versionName}.apk"))
        }
    }
    testVariants.all { variant ->
        variant.outputs.all { output ->
            outputFileName = new File(outputFileName.replace(".apk", "-${defaultConfig.versionName}.apk"))
        }
    }

    packagingOptions {
        // Pick first occurrence of any files that cause conflicts, as defined
        // in common.gradle
        pickFirst 'META-INF/DEPENDENCIES'
        pickFirst 'META-INF/LICENSE'
        pickFirst 'META-INF/LICENSE.txt'
        pickFirst 'META-INF/NOTICE'
        pickFirst 'META-INF/NOTICE.txt'
        pickFirst 'META-INF/services/com.fasterxml.jackson.core.JsonFactory'
        pickFirst 'META-INF/services/com.fasterxml.jackson.core.ObjectCodec'
        pickFirst 'META-INF/services/javax.ws.rs.ext.MessageBodyReader'
        pickFirst 'META-INF/services/javax.ws.rs.ext.MessageBodyWriter'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dexOptions {
        javaMaxHeapSize '2048M'
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
            returnDefaultValues = true
        }
    }
}

configurations.all {
    resolutionStrategy {
        cacheDynamicVersionsFor(0, 'seconds')
        cacheChangingModulesFor(0, 'seconds')
    }
    transitive = true
}

allprojects {
    tasks.withType(JavaCompile) {
        options.compilerArgs << '-Xlint:all'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: '*.jar')
    implementation group: 'com.android.support', name: 'support-v13', version: '27.1.0'
    implementation group: 'com.android.support', name: 'customtabs', version: '27.1.0'
    implementation group: 'com.android.support', name: 'design', version: '27.1.0'
    implementation group: 'com.android.support', name: 'appcompat-v7', version: '27.1.0'
    implementation group: 'com.android.support', name: 'design', version: '27.1.0'
    implementation group: 'com.android.support', name: 'cardview-v7', version: '27.1.0'
    implementation group: 'com.android.support', name: 'multidex', version: '1.0.3'
    implementation group: 'com.android.support', name: 'exifinterface', version: '27.1.0'
    implementation group: 'com.google.android.gms', name: 'play-services-analytics', version: '10.0.1'
    implementation group: 'com.google.android.gms', name: 'play-services-auth', version: '10.0.1'
    implementation group: 'com.google.android.gms', name: 'play-services-maps', version: '10.0.1'
    implementation group: 'com.google.android.gms', name: 'play-services-location', version: '10.0.1'
    implementation(group: 'com.google.code.gson', name: 'gson', version: '2.6.2') {
        exclude group: 'org.apache.httpcomponents'
    }
    implementation group: 'com.google.firebase', name: 'firebase-core', version: '10.0.1'
    implementation group: 'com.crashlytics.sdk.android', name: 'crashlytics', version: '2.9.1'
    implementation(group: 'com.google.http-client', name: 'google-http-client', version: '1.22.0') {
        exclude group: 'org.apache.httpcomponents'
    }
    implementation(group: 'com.google.oauth-client', name: 'google-oauth-client', version: '1.22.0') {
        exclude group: 'org.apache.httpcomponents'
    }

    implementation group: 'com.rarepebble', name: 'colorpicker', version: '2.3.1'
    implementation group: 'commons-io', name: 'commons-io', version: '2.5'
    implementation group: 'net.sf.kxml', name: 'kxml2', version: '2.3.0'
    implementation group: 'net.sf.opencsv', name: 'opencsv', version: '2.3'
    implementation(group: 'org.opendatakit', name: 'opendatakit-javarosa', version: '2.9.0') {
        exclude module: 'joda-time'
    }

    implementation group: 'org.osmdroid', name: 'osmdroid-android', version: '5.6.4'
    implementation group: 'org.slf4j', name: 'slf4j-android', version: '1.6.1-RC1'
    implementation group: 'pub.devrel', name: 'easypermissions', version: '0.2.1'
    implementation(group: 'com.google.api-client', name: 'google-api-client-android', version: '1.22.0') {
        exclude group: 'org.apache.httpcomponents'
    }
    implementation(group: 'com.google.apis', name: 'google-api-services-drive', version: 'v3-rev64-1.22.0') {
        exclude group: 'org.apache.httpcomponents'
    }
    implementation(group: 'com.google.apis', name: 'google-api-services-sheets', version: 'v4-rev463-1.22.0') {
        exclude group: 'org.apache.httpcomponents'
    }
    implementation group: 'com.jakewharton.timber', name: 'timber', version: '4.6.0'
    implementation group: 'com.google.zxing', name: 'core', version: '3.3.0'
    implementation group: 'com.journeyapps', name: 'zxing-android-embedded', version: '3.5.0'
    implementation group: 'net.danlew', name: 'android.joda', version: '2.9.9'

    // Real LeakCanary for debug builds only: notifications, analysis, etc
    debugImplementation group: 'com.squareup.leakcanary', name: 'leakcanary-android', version: '1.5.4'
    // No-Op version of LeakCanary for release builds: no notifications, no analysis, nothing
    releaseImplementation group: 'com.squareup.leakcanary', name: 'leakcanary-android-no-op', version: '1.5.4'
    testImplementation group: 'com.squareup.leakcanary', name: 'leakcanary-android-no-op', version: '1.5.4'
    androidTestImplementation group: 'com.squareup.leakcanary', name: 'leakcanary-android-no-op', version: '1.5.4'
    odkCollectReleaseImplementation group: 'com.squareup.leakcanary', name: 'leakcanary-android-no-op', version: '1.5.4'

    // Android Architecture Components:
    implementation "android.arch.lifecycle:extensions:1.1.0"

    // Dagger:
    implementation 'com.google.dagger:dagger-android:2.11'
    implementation 'com.google.dagger:dagger-android-support:2.11'
    annotationProcessor 'com.google.dagger:dagger-android-processor:2.11'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.11'

    // RxJava 2:
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'
    implementation 'io.reactivex.rxjava2:rxjava:2.1.10'

    // Better "Subjects" for Rx:
    implementation 'com.jakewharton.rxrelay2:rxrelay:2.0.0'

    // Android bindings for Rx:
    implementation 'com.jakewharton.rxbinding2:rxbinding:2.0.0'

    // RxLifecycle (binds subscription cleanup to component lifecycle):
    implementation 'com.trello.rxlifecycle2:rxlifecycle:2.2.1'
    implementation 'com.trello.rxlifecycle2:rxlifecycle-android:2.2.1'
    implementation 'com.trello.rxlifecycle2:rxlifecycle-android-lifecycle:2.2.1'

    // Makes binding to Views easy:
    implementation 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'

    // Used to generate documentation screenshots.
    androidTestCompile 'tools.fastlane:screengrab:1.1.0'

    // Testing-only dependencies
    testImplementation group: 'junit', name: 'junit', version: '4.12'
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '2.8.47'
    testImplementation group: 'org.robolectric', name: 'robolectric', version: '3.5.1'
    testImplementation group: 'org.robolectric', name: 'shadows-multidex', version: '3.5.1'

    // power mock (for mocking final methods which is not handled by mockito)
    testImplementation group: 'org.powermock', name: 'powermock-module-junit4', version: '1.6.4'
    testImplementation group: 'org.powermock', name: 'powermock-api-mockito2', version: '1.7.3'

    androidTestImplementation group: 'org.mockito', name: 'mockito-android', version: '2.11.0'
    androidTestImplementation(group: 'com.android.support.test', name: 'runner', version: '1.0.1') {
        exclude group: 'com.android.support', module: 'support-annotations'
    }

    androidTestImplementation(group: 'com.android.support.test.espresso', name: 'espresso-core', version: '3.0.1') {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
    }

    androidTestImplementation(group: 'com.android.support.test.espresso', name: 'espresso-intents', version: '3.0.1') {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
    }

    androidTestImplementation group: 'com.squareup.okhttp3', name: 'mockwebserver', version: '3.9.0'
}


// Must be at bottom to prevent dependency collisions
// https://developers.google.com/android/guides/google-services-plugin
apply plugin: 'com.google.gms.google-services'



